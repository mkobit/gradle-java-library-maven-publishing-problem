plugins {
  id "com.gradle.build-scan" version "1.6"
}

buildScan {
  licenseAgreementUrl = 'https://gradle.com/terms-of-service'
  licenseAgree = 'yes'
  if (project.hasProperty('useKts')) {
    tag('With KTS')
  } else {
    tag('With Groovy')
  }
}

allprojects {
  group = 'mkobit.example'
  version = '1.0.0'
  repositories {
    jcenter()
  }
}

wrapper {
  gradleVersion = '3.5-rc-3'
}

if (project.hasProperty('useKts')) {
  apply from: 'build.gradle.kts'
} else {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  logger.lifecycle('Applying dependencies before publication creation')
  dependencies {
    api group: 'com.google.guava', name: 'guava', version: '21.0'
    api("org.apache.commons:commons-lang:3.3.5")
    implementation group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.3"
  }

  publishing {
    publications {
      mavenJavaLibrary(MavenPublication) {
        from components.java
      }
    }
  }
  logger.lifecycle('Applying dependencies after publication creation')
  dependencies {
    api group: 'com.google.inject', name: 'guice', version: '4.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  }
}

